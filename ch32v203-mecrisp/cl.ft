\ cl.ft clock routines

#require br.ft
#require led.ft

\res MCU: ch32v2x
\res export RCC RC_CFGR0

: rc.ee ( -- ) 1 16 lshift RCC bis! ; \ hse on
: rc.pe ( -- ) 1 24 lshift RCC bis! ; \ pll on
: rc.pd ( -- ) 1 24 lshift RCC bic! ; \ pll off
: rc.st ( -- u ) RCC @ ;
: rc.cf ( -- u ) RCC RC_CFGR0 + @ ; \ configuration 0


: rc.mul ( u -- ) \ set pll multiplier
  $f 18 lshift RCC RC_CFGR0 + bic!
  18 lshift RCC RC_CFGR0 + bis!
;
: rc.ps ( -- ) \ select pll as the system clock
  3 RCC RC_CFGR0 + bic!
  2 RCC RC_CFGR0 + bis!
;
: rc.xs ( -- ) \ select external osc as the system clock
  3 RCC RC_CFGR0 + bic!
  1 RCC RC_CFGR0 + bis!
;
: rc.hs ( -- ) 3 RCC RC_CFGR0 + bic! ; \ select hsi as the system clock
: rc.es ( -- ) rc.ee 1 16 lshift RCC RC_CFGR0 + bis! ; \ select hse to drive PLL



: hsi rc.hs $45 u1.er rc.pd ;
: hse rc.ee rc.xs $d0 u1.er rc.pd ;

: rc.nx ( u -- ) hsi hse rc.pd rc.es rc.mul rc.pe rc.ps \ mul x HSE
  u1.er
  5bl
;
: rc.ni ( u -- ) hsi rc.pd rc.hs rc.mul rc.pe rc.ps \ mul x HSI/2
  u1.er
  5bl
;
: 16i $8b 2 rc.ni ;
: 24i $d0 4 rc.ni ;
: 72i $271 15 rc.ni ;
: 72e $271 1 rc.nx ;
: 96e $341 2 rc.nx ;
