\ led.ft controls LEDS on my STM32F401RC breakout board.
\ The routines are written in forth.

\res MCU: stm32f401
\res export RCC GPIOC GPIOD RC_AHB1ENR GP_MODER GP_BSRR
\res export GPIOA GP_PUPDR GP_IDR

: rcc.led.en ( -- )
  1 2 lshift 1 3 lshift or RCC RC_AHB1ENR + bis!
  %01 GPIOC GP_MODER + bis!
  %01 2 2 * lshift GPIOD GP_MODER + bis!
;
: rcc.btn.en ( -- )
  1 RCC RC_AHB1ENR + bis! \ GPIOA
  %11 GPIOA GP_PUPDR + bic!
  %01 GPIOA GP_PUPDR + bis! \ pull-up
;
: led1.on ( -- ) 1 2 lshift GPIOD GP_BSRR + bis! 
;
: led1.off ( -- ) 1 2 16 + lshift GPIOD GP_BSRR + bis!
;
: led2.on ( -- ) 1 GPIOC GP_BSRR + bis!
;
: led2.off ( -- ) 1 0 16 +  lshift GPIOC GP_BSRR + bis!
;

\ Toggle line buffering with \^ on eth4com.
: led.kb
  rcc.led.en
  begin
    key dup
    $31 = if led1.on then dup
    $32 = if led1.off then dup
    $33 = if led2.on then dup
    $34 = if led2.off then dup
    $30 =
  until
;

#require st.ft
: 5bl ( -- ) st.init rcc.led.en
  5 0 do 
    led1.on 500 dly led1.off 500 dly
  loop
;

: btn ( -- )
  rcc.led.en rcc.btn.en
  begin
    1 GPIOA GP_IDR + hbit@ if led1.off led2.on else led1.on led2.off then
  key? until
;
